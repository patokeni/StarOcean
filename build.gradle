buildscript {
    repositories {
        maven { url "https://maven.aliyun.com/repository/gradle-plugin" }
    }
}

plugins {
    id('java')
    id('maven-publish')
    id('com.github.johnrengelman.shadow') version '7.1.2'
}

repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenLocal()
    maven { url 'https://maven.aliyun.com/repository/central' } //mavenCentral()
    maven { url 'https://maven.aliyun.com/repository/jcenter' } //jcenter()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots' } //spigot offical repository 
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/sonatype-nexus-snapshots/' }
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
}

dependencies {
    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    //implementation 'com.google.guava:guava:27.0.1-jre'
    
    compileOnly group: 'org.staroceanmc.nebula', name: 'nebula-api', version: '1.20.1-R0.1-SNAPSHOT'
    // compileOnly group: 'xianxian.mc.nebula', name: 'nebula-api', version: '1.15.2-R0.1-SNAPSHOT'
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '18.0.0'
    implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.17'
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '3.4.1'
    implementation group: 'co.aikar', name: 'taskchain-bukkit', version: '3.7.2'
    implementation group: 'co.aikar', name: 'acf-paper', version: '0.5.0-SNAPSHOT'
}

shadowJar {
    archiveClassifier.set("dist")

    relocate 'io.netty', 'xianxian.mc.starocean.libs.netty'
    relocate 'com.google.protobuf', 'xianxian.mc.starocean.libs.protobuf'
    relocate 'org.sqlite', 'xianxian.mc.starocean.libs.sqlite'
    relocate 'google', 'xianxian.mc.starocean.libs.protobuf.google'
    relocate 'com.mysql', 'xianxian.mc.starocean.libs.mysql'
    relocate 'com.zaxxer.hikari', 'xianxian.mc.starocean.libs.hikari'
    relocate 'org.slf4j', 'xianxian.mc.starocean.libs.slf4j'
}

tasks.withType(JavaCompile) {
    options.encoding ='UTF-8'
}

def gitCommitVersion() {
    def cmd = 'git rev-list HEAD --abbrev-commit --max-count=1'
    cmd.execute().text.trim()
}

project.version = project.version + "+" + gitCommitVersion()

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }
}

sourceCompatibility = 17
targetCompatibility = 17
