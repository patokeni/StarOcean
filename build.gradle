buildscript {
    repositories {
        maven { url "https://maven.aliyun.com/repository/gradle-plugin" }
    }
}

plugins {
    id('java')
    id('maven-publish')
    id('com.github.johnrengelman.shadow') version '7.1.2'
}

repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenLocal()
    maven { url 'https://maven.aliyun.com/repository/central' } //mavenCentral()
    maven { url 'https://maven.aliyun.com/repository/jcenter' } //jcenter()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots' } //spigot offical repository 
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/sonatype-nexus-snapshots/' }
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url 'https://repo.codemc.org/repository/maven-public' }
}

dependencies {
    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    //implementation 'com.google.guava:guava:27.0.1-jre'
    
    compileOnly group: 'org.staroceanmc.nebula', name: 'nebula-api', version: '1.20.1-R0.1-SNAPSHOT'
    // compileOnly group: 'xianxian.mc.nebula', name: 'nebula-api', version: '1.15.2-R0.1-SNAPSHOT'
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '18.0.0'
    implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.17'
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '3.4.1'
    implementation group: 'co.aikar', name: 'taskchain-bukkit', version: '3.7.2'
    implementation group: 'co.aikar', name: 'acf-paper', version: '0.5.0-SNAPSHOT'
    implementation group: 'de.tr7zw', name: 'item-nbt-api', version: '2.11.3'
}

shadowJar {
    archiveClassifier.set("dist")

    relocate 'io.netty', 'org.staroceanmc.bukkit.libs.netty'
    relocate 'com.google.protobuf', 'org.staroceanmc.bukkit.libs.protobuf'
    relocate 'org.sqlite', 'org.staroceanmc.bukkit.libs.sqlite'
    relocate 'google', 'org.staroceanmc.bukkit.libs.protobuf.google'
    relocate 'com.mysql', 'org.staroceanmc.bukkit.libs.mysql'
    relocate 'com.zaxxer.hikari', 'org.staroceanmc.bukkit.libs.hikari'
    relocate 'org.slf4j', 'org.staroceanmc.bukkit.libs.slf4j'
    relocate 'de.tr7zw.changeme.nbtapi', 'org.staroceanmc.bukkit.libs.nbtapi'
}

tasks.withType(JavaCompile) {
    options.encoding ='UTF-8'
}

def gitCommitVersion() {
    def cmd = 'git rev-list HEAD --abbrev-commit --max-count=1'
    cmd.execute().text.trim()
}

project.version = project.version + "+" + gitCommitVersion()

print("Current version: " + project.version)

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }
}

sourceCompatibility = 17
targetCompatibility = 17
